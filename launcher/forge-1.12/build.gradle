buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.0.127'
    }
}


apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    flatDir {
        dirs 'libs'
    }
}

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = minecraft_version + "-" + forge_version
    runDir = "run"

    mappings = "snapshot_20171003"
}

configurations
{
    shade {
        transitive = false;
    }

    compile.extendsFrom shade
}

dependencies {
    shade project(':api')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from(project.configurations.shade.files) {
        include '*'
        into 'META-INF/libraries'
    }

    manifest {
        attributes([
            'ContainedDeps': project.configurations.shade.files.collect { it.name }.join(' '),
            'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}:forge-1.12",
            'Timestamp': System.currentTimeMillis(),
            'FMLAT': "jvoxelizer_at.cfg"
        ])
    }
}

